apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = '0.2.0'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false;
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'nl.littlerobots'
            artifactId 'cupboard-tools'
            version project.version
            artifact "${project.buildDir}/outputs/aar/tools-release.aar"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

if (project.has('bintrayUser')) {
    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['maven']

        pkg {
            repo = 'android'
            userOrg = 'littlerobots'
            name = 'cupboard-tools'
            websiteUrl = 'https://bitbucket.org/littlerobots/cupboard-tools'
            vcsUrl = "https://bitbucket.org/littlerobots/cupboard-tools"

            licenses = ['Apache-2.0']
            version {
                name = project.version
            }
        }
    }
}

dependencies {
    compile 'nl.qbusict:cupboard:2.1.4'
    provided 'com.google.code.gson:gson:2.4'
    provided 'com.android.support:support-v4:23.1.0'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
}
